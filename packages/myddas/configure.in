MYDDAS_VERSION=0.9.1
AC_DEFINE_UNQUOTED(MYDDAS_VERSION, $MYDDAS_VERSION, [myddas release])

AC_ARG_WITH(myddas,
	[ --with-myddas[[=DIR]]      with the MYDDAS library],
	if test "$withval" = yes; then
		yap_cv_myddas=/usr
	elif test "$withval" = no; then
		yap_cv_myddas=no
	else
		yap_cv_myddas=$with_myddas
		LDFLAGS="$LDFLAGS -L${yap_cv_myddas}/lib "
		CPPFLAGS="$CPPFLAGS -I${yap_cv_myddas}/include "
		ODBC_LDFLAGS="-L${yap_cv_myddas}/lib "
		ODBC_CFLAGS="-I${yap_cv_myddas}/include "
	fi,
	[yap_cv_myddas=/usr])

AC_ARG_ENABLE(myddas-stats,
	[ --enable-myddas-stats      enable the MYDDAS library statistics support],
	if test "$yap_cv_myddas" = no; then
		myddasstats=no
	else
		myddasstats="$enableval"
	fi, myddasstats=no)

AC_ARG_ENABLE(myddas-top-level,
	[ --enable-myddas-top-level  enable the MYDDAS top-level support to MySQL],
	if test "$yap_cv_myddas" = no; then
	   	myddastoplevel=no
	else
		myddastoplevel="$enableval"
	fi, myddastoplevel=no)

if test "$yap_cv_myddas" != "no"
then
	PKG_MYDDAS="packages/myddas"
	dnl check for mysql
	OLD_LIBS="$LIBS"
	AC_SEARCH_LIBS(mysql_init, [mysqlclient], [yap_cv_mysql="yes" MYDDAS_LIBS="-lmysqlclient $MYDDAS_LIBS"], [yap_cv_mysql="no"])
	AC_CHECK_HEADERS(mysql/mysql.h, [], [yap_cv_mysql="no"])
	if test "$yap_cv_mysql" = yes 
	then
                YAP_EXTRAS="$YAP_EXTRAS -DMYDDAS_MYSQL"
	fi
	
	dnl check for odbc
        case "$target_os" in
	  *darwin*)
	    LIBS="$LIBS -framework CoreFoundation"
	    MYDDAS_LIBS="$MYDDAS_LIBS -framework CoreFoundation"
	;;
	  **)		
	;;
	esac
	AC_SEARCH_LIBS(SQLAllocHandle, [odbc], [yap_cv_odbc="yes"], [yap_cv_odbc="no"])
	if test "$yap_cv_odbc" = yes
	then
	   MYDDAS_LIBS="$MYDDAS_LIBS -lodbc"
	else
	  AC_SEARCH_LIBS(SQLAllocHandle, [iodbc], [yap_cv_odbc="yes"], [yap_cv_odbc="no"])
	  if test "$yap_cv_odbc" = yes
	  then
	     MYDDAS_LIBS="$MYDDAS_LIBS -liodbc"
  	  else
	    AC_SEARCH_LIBS(SQLAllocHandle, [odbc32], [yap_cv_odbc="yes" ], [yap_cv_odbc="no"])
	    if test "$yap_cv_odbc" = yes
	    then
	     MYDDAS_LIBS="$MYDDAS_LIBS -lodbc32"
	    fi
	  fi
        fi
	#from SWI ODBC library.
	AC_CHECK_HEADERS(windef.h)
	AC_CHECK_HEADERS(sql.h, [ ], [yap_cv_odbc="no"])
	if test "$yap_cv_odbc" = yes 
	then
		YAP_EXTRAS="$YAP_EXTRAS -DMYDDAS_ODBC"
	fi
	
	if test "$myddasstats" = "yes"
	  then
	    YAP_EXTRAS="$YAP_EXTRAS -DMYDDAS_STATS"
	fi

	if test "$myddastoplevel" = "yes"
	  then
	    YAP_EXTRAS="$YAP_EXTRAS -DMYDDAS_TOP_LEVEL"
	fi
	LIBS="$OLD_LIBS"
else
	ENABLE_MYDDAS=""
fi	

AC_SUBST(PKG_MYDDAS)
AC_SUBST(MYDDAS_LIBS)


